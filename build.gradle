plugins {
    id 'net.researchgate.release' version '2.8.0'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'com.github.johnrengelman.shadow' version '5.1.0'

}
apply plugin: 'java'
apply plugin: 'maven-publish'


sourceCompatibility = 1.6
targetCompatibility = 1.6

project.ext {
    releaseMode = project.hasProperty("releaseMode")
}

description = 'EPAM Report portal. Client'

repositories {
    jcenter()
    mavenLocal()
    maven { url "http://dl.bintray.com/epam/reportportal" }
    maven { url "https://jitpack.io" }
}

sourceSets {
    main {
        compileClasspath += configurations.shadow
        runtimeClasspath += configurations.shadow
    }
    test {
        compileClasspath += configurations.shadow
        runtimeClasspath += configurations.shadow
    }
}

//configurations.shadow.extendsFrom(configurations.runtime)

shadowJar {
//    configurations = [project.configurations.runtime]

    dependencies {
        include(dependency('com.epam.reportportal:restendpoint'))
        include(dependency('org.apache.httpcomponents:httpclient'))
        include(dependency('org.apache.httpcomponents:httpmime'))
        include(dependency('org.apache.httpcomponents:httpcore'))
        include(dependency('commons-logging:commons-logging'))
        include(dependency('com.google.guava:guava'))

    }
    archiveClassifier.set(null)
    relocate 'org.apache.http', 'rp.org.apache.http'
    relocate 'com.google.common', 'rp.com.google.common'
    relocate 'org.apache.commons.logging', 'rp.org.apache.commons.logging'
}

dependencies {
    if (releaseMode) {
        compile 'com.epam.reportportal:commons-model:3.0.0'
    } else {
        compile 'com.github.reportportal:commons-model:25f966e'
    }

    compile 'com.google.guava:guava:26.0-jre'
    compile 'com.epam.reportportal:restendpoint:0.2.8'
    shadow 'com.fasterxml.jackson.core:jackson-databind:2.9.9'
    shadow 'com.fasterxml.jackson.core:jackson-annotations:2.9.9'
    shadow 'io.reactivex.rxjava2:rxjava:2.2.10'
    shadow 'org.apache.tika:tika-core:1.4'
    shadow 'org.slf4j:slf4j-api:1.7.25'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:2.0.2-beta'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
}

test.useTestNG()
test.maxParallelForks = 1

wrapper {
    gradleVersion = '5.4.1'
}


task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

javadoc() {
    failOnError = false
}


artifacts {
    archives sourcesJar
    archives javadocJar
    archives shadowJar
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
//
//
//            artifact project.tasks.shadowJar
            artifact project.tasks.javadocJar
            artifact project.tasks.sourcesJar

            pom.withXml {
                def root = asNode()
                root.dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.shadow.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each() {
                    it.scope*.value = 'compile'
                }
            }

            groupId 'com.epam.reportportal'
            artifactId "$project.name"

        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['shadow']
    publish = true
    pkg {
        repo = 'reportportal'
        name = "$project.name"
        desc = "$project.description"
        websiteUrl = 'https://reportportal.github.io/'
        issueTrackerUrl = "https://github.com/reportportal/$project.name/issues"
        vcsUrl = "https://github.com/reportportal/$project.name"
        githubRepo = "reportportal/$project.name"
        githubReleaseNotesFile = 'CHANGELOG.md'

        userOrg = 'epam'
        licenses = ['Apache-2.0']
    }
}
afterReleaseBuild.dependsOn bintrayUpload
